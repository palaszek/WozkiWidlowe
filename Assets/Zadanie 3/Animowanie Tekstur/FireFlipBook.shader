Shader "Custom/URP_FireFlipbook"
{
    Properties
    {
        _MainTex ("Atlas (Flipbook)", 2D) = "white" {}
        _Rows    ("Rows", Float)          = 4     // wiersze w atlasie
        _Columns ("Columns", Float)       = 4     // kolumny w atlasie
        _Speed   ("Frames per Second", Float) = 16
    }

    SubShader
    {
        Tags
        {
            "RenderType"="Transparent"
            "Queue"="Transparent"
            "RenderPipeline"="UniversalPipeline"
        }

        Pass
        {
            Name "Forward"
            Tags { "LightMode"="UniversalForward" }

            Blend One One
            ZWrite Off
            Cull Back

            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"

            struct Attributes
            {
                float4 positionOS : POSITION;
                float2 uv         : TEXCOORD0;
            };

            struct Varyings
            {
                float4 positionHCS : SV_POSITION;
                float2 uv          : TEXCOORD0;
            };

            TEXTURE2D(_MainTex);
            SAMPLER(sampler_MainTex);
            float4 _MainTex_ST;

            float _Rows;
            float _Columns;
            float _Speed;

            Varyings vert (Attributes IN)
            {
                Varyings OUT;
                OUT.positionHCS = TransformObjectToHClip(IN.positionOS.xyz);
                OUT.uv = TRANSFORM_TEX(IN.uv, _MainTex);
                return OUT;
            }

            half4 frag (Varyings IN) : SV_Target
            {
                float totalFrames = _Rows * _Columns;

                float frame = floor(_Time.y * _Speed);
                float frameIndex = fmod(frame, totalFrames);

                float col = fmod(frameIndex, _Columns);
                float row = floor(frameIndex / _Columns);

                float2 frameUV = IN.uv;

                frameUV.x /= _Columns;
                frameUV.y /= _Rows;

                frameUV.x += col / _Columns;
                frameUV.y += (_Rows - 1 - row) / _Rows;

                half4 colTex = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, frameUV);
                return half4(colTex.rgb, 1.0);
            }

            ENDHLSL
        }
    }
}
